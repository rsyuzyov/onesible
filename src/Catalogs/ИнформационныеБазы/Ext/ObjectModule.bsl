Функция Адрес() Экспорт

	Возврат СтрШаблон("http://%1:%2@%3:%4/%5/hs/fastex/", Логин(), Пароль(), АдресПубликации(), ПортПубликации(), ИмяПубликации());

КонецФункции

Функция Логин() Экспорт 
	Возврат Логин;
КонецФункции

Функция Пароль() Экспорт 
	Возврат Пароль;
КонецФункции

Функция АдресПубликации() Экспорт 
	Возврат Компьютер.IP;
КонецФункции

Функция ПортПубликации() Экспорт 
	Возврат ?(ПустаяСтрока(ПортПубликации), "80", ПортПубликации);
КонецФункции

Функция ИмяПубликации() Экспорт 
	Возврат ИмяПубликации;
КонецФункции

Процедура ПередЗаписью(Отказ)
	
	Если ПустаяСтрока(Версия) Тогда
		Версия = "?";
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьДанные(Инфо = Неопределено) Экспорт 

	Если Инфо = Неопределено Тогда
		ДополнительныеПараметры = Новый Структура("ПараметрыПреобразованияJSON", Новый Структура("ПрочитатьВСоответствие", Ложь));
		Инфо = КоннекторHTTP.GetJson(Адрес() + "node/info", , ДополнительныеПараметры);
		Если НЕ ТипЗнч(Инфо) = Тип("Структура")  Тогда
			ВызватьИсключение Инфо;
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ СокрЛП(Версия) = СокрЛП(Инфо.version) Тогда
		Версия = ?(ПустаяСтрока(Инфо.version), "?", Инфо.version);
	КонецЕсли;
	
	Набор = РегистрыСведений.Расширения.СоздатьНаборЗаписей();
	Набор.Отбор.ИнформационнаяБаза.Установить(Ссылка, Истина);
	
	Для каждого ОписаниеРасширения Из Инфо.extensions Цикл
		
		РасширениеСсылка = Справочники.Расширения.НайтиПоНаименованию(ОписаниеРасширения.Ключ);
		Если РасширениеСсылка.Пустая() Тогда
			РасширениеОбъект = Справочники.Расширения.СоздатьЭлемент();
			РасширениеОбъект.Наименование = ОписаниеРасширения.Ключ;
			РасширениеОбъект.Записать();
			РасширениеСсылка = РасширениеОбъект.Ссылка;
		КонецЕсли;
		
		Запись = Набор.Добавить();
		Запись.Активность = Истина;
		Запись.ИнформационнаяБаза = Ссылка;
		Запись.Расширение = РасширениеСсылка;
		Запись.Версия =  ?(ПустаяСтрока(ОписаниеРасширения.Значение), "?", ОписаниеРасширения.Значение);
		
	КонецЦикла;
	
	//Сортировка для сохранения порядка записей - важно для проверки измененности
	Таблица = Набор.Выгрузить();
	Таблица.Сортировать("Расширение");
	Набор.Загрузить(Таблица);
	
	СодержаниеВерсий = "";
	Если ПроверкаИзмененности.ОбъектИзменен(Набор,,,,СодержаниеВерсий) Тогда
 		Набор.Записать();
	КонецЕсли;
	
	СодержаниеВерсий = "";
	Если ПроверкаИзмененности.ОбъектИзменен(ЭтотОбъект,,,,СодержаниеВерсий) Тогда
		Записать();
	КонецЕсли;
	
	Компьютер.ПолучитьОбъект().ОбновитьДанные(Инфо);
	
КонецПроцедуры

