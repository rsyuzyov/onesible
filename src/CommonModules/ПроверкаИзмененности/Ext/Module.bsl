//Возвращает изменность объекта по сравнению с сохраненной версией
//Источник - ДокументОбъект, СправочникОбъект.
//РежимЗаписи - РежимЗаписиДокумента.
//ИсключаемыеРеквизиты - Строка. Список реквизитов, несущественных при определении изменности
//СодержаниеВерсий - Структура. Содержит описание сохраненной и текущией версии для формирования отчета о сравнении объектов
Функция ОбъектИзменен(Источник, Знач РежимЗаписи = Неопределено, Знач ВыполнятьПовторнуюПроверку = Истина, Знач ИсключаемыеРеквизиты = "", СодержаниеВерсий = Неопределено) Экспорт

	Если Источник.ДополнительныеСвойства.Свойство("ОбъектИзменен") И НЕ ВыполнятьПовторнуюПроверку Тогда
		Возврат Источник.ДополнительныеСвойства.ОбъектИзменен;
	КонецЕсли;
	
	СодержаниеВерсий = Новый Структура("ИсходнаяВерсия, ТекущаяВерсия");
	
	Если Источник = Неопределено Тогда
		СодержаниеВерсий.ИсходнаяВерсия = "Неопределено";
		СодержаниеВерсий.ТекущаяВерсия = "";
		Источник.ДополнительныеСвойства.Вставить("ОбъектИзменен", Истина);
		Возврат Истина;
	КонецЕсли;
	
	ТипЗначения = ТипЗнч(Источник);
	ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипЗначения);
	
	Если Метаданные.РегистрыНакопления.Содержит(ОбъектМетаданных) ИЛИ Метаданные.РегистрыСведений.Содержит(ОбъектМетаданных) Тогда
		ИсточникДо = КопияОбъекта(Источник);
		ИсточникДо.Прочитать();
		ЗаполнитьСодержаниеВерсий(СодержаниеВерсий, Источник, ИсточникДо, ИсключаемыеРеквизиты);
	ИначеЕсли НЕ Метаданные.Справочники.Содержит(ОбъектМетаданных) И НЕ Метаданные.Документы.Содержит(ОбъектМетаданных) Тогда
		СодержаниеВерсий.ИсходнаяВерсия = "" + ТипЗнч(Источник) + " не является справочником или документом";
		СодержаниеВерсий.ТекущаяВерсия = "";
		Источник.ДополнительныеСвойства.Вставить("ОбъектИзменен", Истина);
	ИначеЕсли Источник.ЭтоНовый() Тогда
		СодержаниеВерсий.ИсходнаяВерсия = "Новый объект";
		СодержаниеВерсий.ТекущаяВерсия = "";
		Источник.ДополнительныеСвойства.Вставить("ОбъектИзменен", Истина);
	ИначеЕсли Источник.Ссылка = Неопределено Тогда
		СодержаниеВерсий.ИсходнаяВерсия = "Новый объект";
		СодержаниеВерсий.ТекущаяВерсия = "";
		Источник.ДополнительныеСвойства.Вставить("ОбъектИзменен", Истина);
	ИначеЕсли РежимЗаписи = РежимЗаписиДокумента.Проведение И НЕ Источник.Проведен Тогда
		СодержаниеВерсий.ИсходнаяВерсия = "Не проведен";
		СодержаниеВерсий.ТекущаяВерсия = "Проведен";
		Источник.ДополнительныеСвойства.Вставить("ОбъектИзменен", Истина);
	ИначеЕсли РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения И Источник.Проведен Тогда
		СодержаниеВерсий.ИсходнаяВерсия = "Проведен";
		СодержаниеВерсий.ТекущаяВерсия = "Не проведен";
		Источник.ДополнительныеСвойства.Вставить("ОбъектИзменен", Истина);
	Иначе
		ЗаполнитьСодержаниеВерсий(СодержаниеВерсий, Источник, Источник.Ссылка.ПолучитьОбъект(), ИсключаемыеРеквизиты);
	КонецЕсли;
	
	Возврат Источник.ДополнительныеСвойства.ОбъектИзменен;

КонецФункции

Процедура ЗаполнитьСодержаниеВерсий(СодержаниеВерсий, Источник, ИсточникДо, ИсключаемыеРеквизиты)

	ТипЗначения = ТипЗнч(Источник);
	ОбъектМетаданных = Метаданные.НайтиПоТипу(ТипЗначения);
	
	Запись = Новый ЗаписьXML;
	Запись.УстановитьСтроку();
	ЗаписатьXML(Запись, ИсточникДо);
	ИсходноеСодержание = Запись.Закрыть();
	
	Запись.УстановитьСтроку();
	ПроверкаИзмененности_ПМ.ВызватьСобытияПередЗаписью(Источник);
	ЗаписатьXML(Запись, Источник);
	ТекущееСодержание = Запись.Закрыть();
	
	ПроверкаИзмененности_ПМ.ДополнитьИсключаемыеРеквизиты(Источник, ИсключаемыеРеквизиты);
	Если НЕ ПустаяСтрока(ИсключаемыеРеквизиты) Тогда
		ОбработкаСтрок.УдалитьУзлыXML(ИсключаемыеРеквизиты, ИсходноеСодержание);
		ОбработкаСтрок.УдалитьУзлыXML(ИсключаемыеРеквизиты, ТекущееСодержание);
	КонецЕсли;
	
	Источник.ДополнительныеСвойства.Вставить("ОбъектИзменен", НЕ ИсходноеСодержание = ТекущееСодержание);
	СодержаниеВерсий.ИсходнаяВерсия = ИсходноеСодержание;
	СодержаниеВерсий.ТекущаяВерсия = ТекущееСодержание;

КонецПроцедуры

Функция КопияОбъекта(Знач Объект)

	Запись = Новый ЗаписьXML;
	Запись.УстановитьСтроку();
	ЗаписатьXML(Запись, Объект);
	Содержание = Запись.Закрыть();
	
	Чтение = Новый ЧтениеXML;
	Чтение.УстановитьСтроку(Содержание);
	Результат = ПрочитатьXML(Чтение);
	Чтение.Закрыть();
	
	Возврат Результат;

КонецФункции
